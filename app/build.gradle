plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'com.google.protobuf'
    id 'idea'
}

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "com.clearkeep.ck"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerVersion "1.3.70-dev-withExperimentalGoogleExtensions-20200424"
        kotlinCompilerExtensionVersion "0.1.0-dev13"
    }

}
protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.11.4'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.27.1'
        }
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }

            task.plugins {
                grpc {
                    // Options added to --grpc_out
                    option 'lite'
                }
            }
        }
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation('androidx.activity:activity-ktx:1.1.0')
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.7"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7'
    // Jetpack Compose
    implementation("androidx.compose:compose-runtime:$jetpack_compose_version")
    implementation("androidx.ui:ui-layout:$jetpack_compose_version")
    implementation("androidx.ui:ui-material:$jetpack_compose_version")
    implementation("androidx.ui:ui-foundation:$jetpack_compose_version")
    implementation("androidx.ui:ui-animation:$jetpack_compose_version")
    implementation "androidx.ui:ui-tooling:$jetpack_compose_version"
    // gRPC
    implementation 'com.squareup.okhttp:okhttp:2.7.5'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation "io.grpc:grpc-core:$grpcVersion"
    implementation "io.grpc:grpc-stub:$grpcVersion"
    implementation "io.grpc:grpc-okhttp:$grpcVersion"
    implementation("io.grpc:grpc-protobuf-lite:$grpcVersion") {
        exclude module: "protobuf-lite"
    }
    //Signal
    implementation("org.whispersystems:signal-protocol-android:$signal_version") {
        exclude group: 'com.google.protobuf', module: 'protobuf-java'
    }
    // Room components
    implementation "androidx.room:room-runtime:$room_db_version"
    implementation "androidx.room:room-ktx:$room_db_version"
    kapt "androidx.room:room-compiler:$room_db_version"
    androidTestImplementation "androidx.room:room-testing:$room_db_version"
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

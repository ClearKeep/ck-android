plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'com.google.protobuf'
    id 'kotlin-kapt'
}

android {
    compileSdk 31

    defaultConfig {
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
        }
    }

    flavorDimensions "version"
    productFlavors {
        prod {
            dimension "version"
            resValue 'string', 'app_name', "ClearKeep"
            buildConfigField "String", 'BASE_URL', "\"ckserver.org\""
            buildConfigField "int", 'PORT', "25000"
            buildConfigField "String", 'BASE_JANUS_URL', "\"54.235.68.160\""
            buildConfigField "int", 'JANUS_PORT', "8188"
        }
        staging {
            dimension "version"
            versionNameSuffix "-staging"
            resValue 'string', 'app_name', "ClearKeep"
            buildConfigField "String", 'BASE_URL', "\"54.235.68.160\""
            buildConfigField "int", 'PORT', "25000"
            buildConfigField "String", 'BASE_JANUS_URL', "\"54.235.68.160\""
            buildConfigField "int", 'JANUS_PORT', "18188"
        }
        dev {
            dimension "version"
            versionNameSuffix "-dev"
            resValue 'string', 'app_name', "CK Dev"
            buildConfigField "String", 'BASE_URL', "\"ckserver.org\""
            buildConfigField "int", 'PORT', "25000"
            buildConfigField "String", 'BASE_JANUS_URL', "\"54.235.68.160\""
            buildConfigField "int", 'JANUS_PORT', "28188"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.11.4'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.27.1'
        }
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }

            task.plugins {
                grpc {
                    // Options added to --grpc_out
                    option 'lite'
                }
            }
        }
    }
}

dependencies {
    implementation project(":domain")
    implementation project(":common")

    implementation Dependencies.androidCore
    implementation Dependencies.appCompat
    implementation Dependencies.materialDesign

    testImplementation Dependencies.junit
    androidTestImplementation Dependencies.junitExt
    androidTestImplementation Dependencies.espresso

    // gRPC
    implementation Dependencies.okhttp
    implementation Dependencies.grpcStub
    implementation Dependencies.grpcOkhttp
    implementation(Dependencies.grpcProtoBufUtil) {
        exclude module: "protobuf-lite"
    }

    implementation Dependencies.dagger
    kapt Dependencies.daggerAndroidProcessor
    implementation Dependencies.daggerAndroidSupport
    kapt Dependencies.daggerCompiler
    compileOnly Dependencies.dagger2InjectAnnotation
    kapt Dependencies.dagger2InjectProcessor
    implementation Dependencies.hilt
    kapt Dependencies.hiltCompiler
    kapt Dependencies.hiltCompilerAndroidx
    implementation Dependencies.coroutinesAndroid
    implementation Dependencies.javaxAnnotations
    implementation Dependencies.protoParser

    //Signal
    implementation("org.signal:libsignal-client:$signal_version")
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
}